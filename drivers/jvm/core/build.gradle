plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.2'
    id 'groovy'
    id 'au.com.dius.pact' version '4.5.4'
}

ext {
    description = "Pact Plugin Driver core implementation"
    gitSha = System.getenv("GIT_SHA") ?: '0'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.21'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.8.21'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'io.grpc:grpc-protobuf:1.53.0'
    implementation 'io.grpc:grpc-stub:1.53.0'
    implementation 'io.grpc:grpc-netty:1.53.0'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation 'com.google.guava:guava:31.1-jre'
    implementation('io.github.microutils:kotlin-logging:3.0.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation 'javax.json:javax.json-api:1.1.4'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'au.com.dius.pact.core:support:4.5.4'
    implementation 'au.com.dius.pact.core:model:4.5.4'
    implementation 'org.apache.tika:tika-core:2.7.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2.1'
    implementation 'com.vdurmont:semver4j:3.1.0'

    protobuf files('../../../proto/')

    testImplementation 'org.apache.groovy:groovy:4.0.10'
    testImplementation 'org.apache.groovy:groovy-json:4.0.10'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.2'
    testImplementation 'org.objenesis:objenesis:3.3'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.5.4'
    testImplementation('au.com.dius.pact.consumer:groovy:4.5.4') {
        transitive = false
    }
    testImplementation 'ch.qos.logback:logback-classic:1.4.6'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
    testImplementation 'org.json:json:20230227'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.21.12" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.53.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    // Set the load plugin timeout to 30 seconds for Windows
    systemProperty('pact.plugin.loadTimeoutInMs', '30000')
}

pact {
    publish {
        pactBrokerUrl = 'https://pact-foundation.pactflow.io'
        consumerVersion = "${project.version}+${gitSha}"
        if (project.hasProperty('pactBrokerToken')) {
            pactBrokerToken = project.pactBrokerToken
        }
    }
}
