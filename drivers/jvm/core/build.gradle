plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.17'
    id 'groovy'
    id 'au.com.dius.pact' version '4.3.0-beta.5'
}

ext {
    description = "Pact Plugin Driver core implementation"
    gitSha = System.getenv("GIT_SHA") ?: '0'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.31'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.31'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'io.grpc:grpc-protobuf:1.41.0'
    implementation 'io.grpc:grpc-stub:1.41.0'
    implementation 'io.grpc:grpc-netty:1.41.0'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation('io.github.microutils:kotlin-logging:2.0.11') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation('com.michael-bull.kotlin-result:kotlin-result:1.1.12') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation 'javax.json:javax.json-api:1.1.4'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'au.com.dius.pact.core:support:4.3.0-beta.6'
    implementation 'au.com.dius.pact.core:model:4.3.0-beta.6'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.1'

    protobuf files('../../../proto/')

    testImplementation "org.codehaus.groovy:groovy:3.0.8"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.8.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.11.18'
    testImplementation 'org.objenesis:objenesis:3.2'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.3.0-beta.6'
    testImplementation 'ch.qos.logback:logback-classic:1.2.6'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.12.0" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.40.1" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

pact {
    publish {
        pactBrokerUrl = 'https://pact-foundation.pact.dius.com.au'
        consumerVersion = "${project.version}+${gitSha}"
        if (project.hasProperty('pactBrokerToken')) {
            pactBrokerToken = project.pactBrokerToken
        }
    }
}
