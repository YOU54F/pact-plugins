plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.17'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.18.1'
    implementation 'io.grpc:grpc-protobuf:1.41.0'
    implementation 'io.grpc:grpc-stub:1.41.0'
    implementation 'io.grpc:grpc-netty:1.41.0'
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    protobuf files('../../../proto/')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.3.2'
    testImplementation 'ch.qos.logback:logback-classic:1.2.8'
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.18.0" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.41.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
