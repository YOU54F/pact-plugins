plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

repositories {
    mavenCentral()
    mavenLocal()
}

targetCompatibility = '11'
sourceCompatibility = '11'

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk9:1.8.1'

    implementation 'com.google.protobuf:protobuf-java:3.25.4'
    implementation 'com.google.protobuf:protobuf-kotlin:3.25.4'
    implementation 'io.grpc:grpc-protobuf:1.56.1'
    implementation 'io.grpc:grpc-netty:1.56.1'
    implementation 'io.grpc:grpc-stub:1.56.1'
    implementation 'io.grpc:grpc-kotlin-stub:1.4.1'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation('io.github.microutils:kotlin-logging:2.1.23') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation 'ch.qos.logback:logback-classic:1.5.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    testImplementation 'au.com.dius.pact.provider:junit5:4.5.12'

    protobuf files('../../proto/')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'io.pact.example.grpc.provider.ServerKt'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.25.4" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.65.1" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.1:jdk8@jar" }
    }
    generateProtoTasks {
        all().forEach {
          it.plugins {
              grpc {}
              grpckt {}
          }
          it.builtins {
              kotlin {}
          }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        kotlin {
            srcDirs 'build/generated/source/proto/main/grpckt'
        }
    }
}
