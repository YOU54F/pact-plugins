plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'application'
    id 'com.google.protobuf' version '0.8.17'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk9:1.6.4'

    implementation 'com.google.protobuf:protobuf-java:3.21.1'
    implementation 'io.grpc:grpc-protobuf:1.48.1'
    implementation 'io.grpc:grpc-netty:1.48.1'
    implementation 'io.grpc:grpc-stub:1.48.1'
    implementation 'io.grpc:grpc-kotlin-stub:1.3.0'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation('io.github.microutils:kotlin-logging:2.1.23') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation 'ch.qos.logback:logback-classic:1.2.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.4.2'

    protobuf files('../../proto/')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'io.pact.example.grpc.provider.ServerKt'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.21.1" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.48.1" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:1.1.0:jdk7@jar" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        kotlin {
            srcDirs 'build/generated/source/proto/main/grpckt'
        }
    }
}
